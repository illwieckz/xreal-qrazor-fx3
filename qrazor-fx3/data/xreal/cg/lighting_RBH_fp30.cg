/// ============================================================================
/*
Copyright (C) 2004 Robert Beckebans <trebor_7@users.sourceforge.net>
Please see the file "AUTHORS" for a list of contributors

This program is free software; you can redistribute it and/or
modify it under the terms of the GNU General Public License
as published by the Free Software Foundation; either version 2
of the License, or (at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  

See the GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.
*/
/// ============================================================================

#include "utils.cg"

struct cg_vertex2fragment
{	
	float4 position		: TEXCOORD0;
	float4 tex_diffuse_bump	: TEXCOORD1;
	float4 tex_height	: TEXCOORD2;
	float4 tex_light	: TEXCOORD3;
	float4 tex_deluxe	: TEXCOORD4;
	
	float3 tangent		: TEXCOORD5;
	float3 binormal		: TEXCOORD6;
	float3 normal		: TEXCOORD7;
};

struct cg_fragment2final
{
	half4 color		: COLOR;
};


cg_fragment2final main(cg_vertex2fragment IN,
			uniform sampler2D diffusemap,
			uniform sampler2D bumpmap,
			uniform sampler2D heightmap,
			uniform sampler2D lightmap,
			uniform sampler2D deluxemap,
			uniform float3 view_origin,
			uniform float bump_scale,
			uniform float height_scale,
			uniform float height_bias)
{
	cg_fragment2final OUT;
	
	// construct object-space-to-tangent-space 3x3 matrix
	float3x3 rotation = float3x3(IN.tangent, IN.binormal, IN.normal);
	
	// compute view direction in tangent space
	fixed3 V = normalize(mul(rotation, view_origin - IN.position.xyz));
	
	// compute height
	half height = tex2D(heightmap, IN.tex_height.xy).x;
	
	// compute texcoords offset
	float2 tex_offset = (height * height_scale + height_bias) * V.xy;
	
	// compute light direction in tangent space from deluxemap
	fixed3 L = normalize(mul(rotation, CG_Expand(tex2D(deluxemap, IN.tex_deluxe.xy).xyz)));
	
	// compute normal in tangent space from bumpmap
	half3 T = CG_Expand(tex2D(bumpmap, IN.tex_diffuse_bump.zw + tex_offset).xyz);
	T.z *= bump_scale;
	fixed3 N = normalize(T);
	
	// compute light color from object space lightmap
	fixed3 C = tex2D(lightmap, IN.tex_light.xy).xyz;
	
	// compute the diffuse term
	fixed4 diffuse = tex2D(diffusemap, IN.tex_diffuse_bump.xy + tex_offset);
	diffuse.rgb *= C * saturate(dot(N, L));
					
	// compute final color
	OUT.color.rgba = diffuse;

	return OUT;
}
